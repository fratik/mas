plugins {
    id 'java'
    id 'application'
    id "io.freefair.lombok" version "6.6.1"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id("io.papermc.paperweight.userdev") version "1.5.5"
}

group = 'pl.fratik'
allprojects {
    version = '1.0.1-SNAPSHOT'
}
mainClassName = 'pl.fratik.mcs.Bootstrap'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    implementation 'io.netty:netty-all:4.1.86.Final'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'it.unimi.dsi:fastutil:8.5.6'
    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'org.slf4j:slf4j-api:2.0.5'
    implementation 'ch.qos.logback:logback-classic:1.4.5'
    implementation 'org.tuxdude.logback.extensions:logback-colorizer:1.0.1'
//    compileOnly 'org.spigotmc:spigot-api:1.19.2-R0.1-SNAPSHOT' // odkomentuj mnie aby sprawdzić kompatybilność ze spigotem
    paperweight.paperDevBundle('1.19.4-R0.1-SNAPSHOT') // domyślnie targetuj papera
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
    options.encoding = 'UTF-8'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.assemble {
    dependsOn(reobfJar)
}
